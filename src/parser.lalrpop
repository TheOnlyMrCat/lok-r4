use either::{Either, Left, Right};
use crate::LexError;
use crate::lexer;
use crate::codegen::ast;

grammar;

extern {
	type Location = usize;
	type Error = LexError;

	enum lexer::Token {
		ID => lexer::Token::Identifier(<String>),
		INT => lexer::Token::Integer(<String>),
		FLOAT => lexer::Token::Float(<String>),

		STRING => lexer::Token::LokStaticString(<Vec<u8>>),
		HSTRING => lexer::Token::LokHeapString(<Vec<u8>>),
		CSTRING => lexer::Token::CStaticString(<Vec<u8>>),
		CHSTRING => lexer::Token::CHeapString(<Vec<u8>>),
		BSTRING => lexer::Token::ByteStaticString(<Vec<u8>>),
		BHSTRING => lexer::Token::ByteHeapString(<Vec<u8>>),

		"let" => lexer::Token::Let,
		"const" => lexer::Token::Const,
		"mut" => lexer::Token::Mut,
		"dyn" => lexer::Token::Dyn,
		"fn" => lexer::Token::Fn,
		"use" => lexer::Token::Use,
		"extern" => lexer::Token::Extern,
		"return" => lexer::Token::Return,
		"yield" => lexer::Token::Yield,
		"break" => lexer::Token::Break,
		"if" => lexer::Token::If,
		"else" => lexer::Token::Else,
		"loop" => lexer::Token::Loop,
		"static" => lexer::Token::Static,
		"entry" => lexer::Token::Entry,

		"(" => lexer::Token::OpenPar,
		")" => lexer::Token::ClosePar,
		"{" => lexer::Token::OpenBrace,
		"}" => lexer::Token::CloseBrace,
		"[" => lexer::Token::OpenBracket,
		"]" => lexer::Token::CloseBracket,
		":" => lexer::Token::Colon,
		"::" => lexer::Token::DblColon,
		";" => lexer::Token::Semicolon,
		"." => lexer::Token::Dot,
		"..." => lexer::Token::TplDot,
		"," => lexer::Token::Comma,
		"+" => lexer::Token::Plus,
		"-" => lexer::Token::Hyphen,
		"*" => lexer::Token::Star,
		"/" => lexer::Token::Slash,
		"%" => lexer::Token::Percent,
		"=" => lexer::Token::Equals,
		"==" => lexer::Token::DblEquals,
		">" => lexer::Token::Greater,
		">=" => lexer::Token::GreaterEqual,
		"<" => lexer::Token::Less,
		"<=" => lexer::Token::LessEqual,

		"->" => lexer::Token::SingleArrow,
		"=>" => lexer::Token::DoubleArrow,
	}
}

pub LokFile: Vec<ast::TopLevelDecl> = {
	<TopLevelDecl*> => <>
}

TopLevelDecl: ast::TopLevelDecl = {
	"extern" "fn" <name:ID> "(" <params:Comma<((<ID> ":")? Type)>> <varadic:"..."?> ")" <returns:("->" <Type>)?> ";" => {
		ast::TopLevelDecl::FnExtern(ast::FnExtern {
			name,
			params,
			varadic: varadic.is_some(),
			returns,
		})
	},
	<TopLevelDef> => ast::TopLevelDecl::Def(<>),
	<Decl> => ast::TopLevelDecl::Decl(<>),
}

Decl: ast::Decl = {
	<external:"extern"?> "use" <ty:("(" <STRING> ")")?> <path:STRING> => {
		ast::Decl::Use(ast::Use {
			external: external.is_some(),
			ty,
			path,
		})
	}
}

TopLevelDef: ast::TopLevelDef = {
	"entry" <returns:("->" <Type>)?> <body:Block> => {
		ast::TopLevelDef::Entry(ast::Entry {
			returns,
			body
		})
	},
	<Def> => ast::TopLevelDef::Def(<>),
}

Def: ast::Def = {
	"fn" <name:ID> "(" <params:Comma<((<ID> ":") Type)>> ")" <returns:("->" <Type>)?> <body:Block> => {
		ast::Def::Fn(ast::FnDef {
			name,
			params,
			returns,
			body,
		})
	}
}

Block: ast::Block = {
	<TaillessBlock> => <>,
	<TailedBlock> => <>,
}

TailedBlock: ast::Block = {
	"{" <statements:Statement*> <tail:Expression> "}" => {
		ast::Block {
			statements,
			tail: Some(tail),
		}
	},
}

TaillessBlock: ast::Block = {
	"{" <statements:Statement*> "}" => {
		ast::Block {
			statements,
			tail: None,
		}
	}
}

TailedIf: ast::If = {
	"if" <cond:Expression> <if_true:TailedBlock> <if_false:TailedElse> => ast::If(Box::new(cond), Box::new(if_true), Some(if_false))
}

TailedElse: Either<Box<ast::If>, Box<ast::Block>> = {
	"else" <TailedIf> => Left(Box::new(<>)),
	"else" <TailedBlock> => Right(Box::new(<>)),
}

TaillessIf: ast::If = {
	"if" <cond:Expression> <if_true:TaillessBlock> <if_false:TaillessElse?> => ast::If(Box::new(cond), Box::new(if_true), if_false)
}

TaillessElse: Either<Box<ast::If>, Box<ast::Block>> = {
	"else" <TaillessIf> => Left(Box::new(<>)),
	"else" <TaillessBlock> => Right(Box::new(<>)),
}

Statement: ast::Statement = {
	<TaillessBlock> => {
		ast::Statement::Expression(ast::Expression::Block(Box::new(<>)))
	},
	<TaillessIf> => {
		ast::Statement::Expression(ast::Expression::If(<>))
	},
	<Expression> ";" => {
		ast::Statement::Expression(<>)
	},
	"let" <m:"mut"?> <n:ID> <t:(":" <Type>)?> "=" <e:Expression> ";" => {
		ast::Statement::Decl {
			name: n,
			mutable: m.is_some(),
			expected_type: t,
			value: e,
		}
	},
	"break" <Expression?> ";" => {
		ast::Statement::Break(<>)
	},
	"return" <Expression?> ";" => {
		ast::Statement::Return(<>)
	},
}

Expression: ast::Expression = {
	<AssignExpression> => <>,
}

AssignExpression: ast::Expression = {
	<lhs:LValueExpression> "=" <rhs:CompareExpression> => ast::Expression::Assign(Box::new(lhs), None, Box::new(rhs)),
	<CompareExpression> => <>,
}

CompareExpression: ast::Expression = {
	<lhs:CompareExpression> "<" <rhs:SumExpression>  => ast::Expression::Op(ast::Op::Lt, Box::new(lhs), Box::new(rhs)),
	<lhs:CompareExpression> "<=" <rhs:SumExpression> => ast::Expression::Op(ast::Op::Le, Box::new(lhs), Box::new(rhs)),
	<lhs:CompareExpression> "==" <rhs:SumExpression> => ast::Expression::Op(ast::Op::Eq, Box::new(lhs), Box::new(rhs)),
	<lhs:CompareExpression> ">=" <rhs:SumExpression> => ast::Expression::Op(ast::Op::Ge, Box::new(lhs), Box::new(rhs)),
	<lhs:CompareExpression> ">" <rhs:SumExpression>  => ast::Expression::Op(ast::Op::Gt, Box::new(lhs), Box::new(rhs)),
	<SumExpression> => <>
}

SumExpression: ast::Expression = {
	<lhs:SumExpression> "+" <rhs:FactorExpression> => ast::Expression::Op(ast::Op::Add, Box::new(lhs), Box::new(rhs)),
	<lhs:SumExpression> "-" <rhs:FactorExpression> => ast::Expression::Op(ast::Op::Sub, Box::new(lhs), Box::new(rhs)),
	<FactorExpression> => <>
}

FactorExpression: ast::Expression = {
	<lhs:FactorExpression> "*" <rhs:CallExpression> => ast::Expression::Op(ast::Op::Mul, Box::new(lhs), Box::new(rhs)),
	<lhs:FactorExpression> "/" <rhs:CallExpression> => ast::Expression::Op(ast::Op::Div, Box::new(lhs), Box::new(rhs)),
	<lhs:FactorExpression> "%" <rhs:CallExpression> => ast::Expression::Op(ast::Op::Rem, Box::new(lhs), Box::new(rhs)),
	<CallExpression> => <>
}

CallExpression: ast::Expression = {
	<e:CallExpression> "(" <p:Comma<Expression>> ")" => ast::Expression::Call(Box::new(e), p),
	<ValueExpression> => <>,
}

ValueExpression: ast::Expression = {
	<INT> => ast::Expression::Int(str::parse(&<>).unwrap()), //TODO: Allow negatives
	<LValueExpression> => <>,
	<CSTRING> => ast::Expression::CStringRef(<>),
	"loop" <TaillessBlock> => ast::Expression::Loop(Box::new(<>)),
	"(" <Expression> ")" => <>,
}

LValueExpression: ast::Expression = {
	<TailedBlock> => ast::Expression::Block(Box::new(<>)),
	<TailedIf> => ast::Expression::If(<>),
	<NSIdent> => ast::Expression::LVar(<>),
}

Type: ast::Type = {
	<NSIdent> => ast::Type::Name(<>),
	"*" "const" <Type> => ast::Type::PtrConst(Box::new(<>)),
	"*" "mut" <Type> => ast::Type::PtrMut(Box::new(<>)),
	"*" "dyn" "const" <Type> => ast::Type::PtrDynConst(Box::new(<>)),
	"*" "dyn" "mut" <Type> => ast::Type::PtrDynMut(Box::new(<>)),
	"[" <Type> "]" => ast::Type::Slice(Box::new(<>)),
	"[" <t:Type> ";" <n:INT> "]" => ast::Type::Arr(Box::new(t), str::parse(&n).unwrap()), //TODO: Add error type for if negative
	"(" <Type*> ")" => ast::Type::Tuple(<>),
}

NSIdent: ast::NSIdent = {
	<mut v:(<ID> "::")*> <e:ID> => {
		v.push(e);
		v
	}
}

Comma<T>: Vec<T> = {
	<mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}